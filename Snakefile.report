#!/bin/bash
# vim: ft=python

# Contents >>>
#   + Embedded BASH script to bootstrap the workflow
#   + Initialisation and configuration
#   + Helper functions
#   + The rules specific to this workflow
#   + More generic rules

# Note this workflow is designed to run locally. All of the heavy lifting should
# have been done by Snakefile.process_cells. It's vaguely useful to have locking here
# as we might possibly try to make 2 interim reports at once (in which case, just let
# one fail).

"""true" ### Begin shell script part
set -u

source "`dirname $0`"/shell_helper_functions.sh

export TOOLBOX="$(find_toolbox)"
export TEMPLATES="$(find_templates)"
export PATH="${PATH}:$(dirname "$0")"

snakerun_single "$0" "$@"

"exit""" ### End of shell script part
#!/usr/bin/env snakemake
from snakemake.utils import format
from smrtino import glob, load_yaml

TOOLBOX = 'env PATH="{}:$PATH"'.format(os.environ['TOOLBOX'])

""" Report will be made based upon all the .info.yml files found. There may be none.
    For every .info.yml we need to generate a corresponding .link.yml, and
    we're also adding in PDF reports from SMRTLink at this stage - other plots are
    generated by Snakefile.process_cells and linked in the info.yml files.
    Also we may or may not have some plots to add.
"""
INFO_YML = glob("*.info.yml")
CELL_NAMES   = [ re.sub(r"\.info\.yml$", "", f) for f in INFO_YML ]

# The report filenames are to be of the form:
#   {cellnum}_{cellid}.pan.html
REP_NAMES  = [ f"{n}_{c}" for n, c in enumerate(CELL_NAMES, start=1) ]

# Global wildcard patterns
wildcard_constraints:
    cellidx    = r"\d+",
    cell       = r"m\w+_\w+_\w+",

# We shall make a single report per cell (used to be one per run).
rule report_main:
    input:
        pan   = expand("all_reports/{r}.pan.html", r=REP_NAMES),
        plist = "projects_ready.txt",

# A list of projects, like with Illuminatus. Used to quickly scan for runs that have data related to
# a project. If there is no valid project name we still want a report but it should be clear there is
# a problem.
localrules: list_projects
rule list_projects:
    output: "projects_ready.txt"
    input: INFO_YML
    priority: 100
    run:
        plist = set()
        for y in input:
            try:
                plist.add(load_yaml(y)['ws_project'])
            except KeyError:
                logger.error("No ws_project in {} - indicates no 5-digit project name found in readset XML".format(y))
        with open(output[0], "w") as ofh:
            print( *sorted(plist), file=ofh, sep='\n' )

# Note this script relies on ~/.smrtlinkrc for connection deets.
localrules: link_to_smrtlink, smrtlink_pdf_report
rule link_to_smrtlink:
    output: "{cell}.link.yml"
    input:  "{cell}.info.yml"
    shell:
        "link_to_smrtlink.py {input} > {output}"

# As does this, which will trigger generation of a new report, poll until ready, then
# download it.
# Modified to proceed even if report generation fails, since some datasets don't get
# properly imported in SMRTLink.
rule smrtlink_pdf_report:
    output: "all_reports/pdf/{cell}.pdf"
    input:  "{cell}.link.yml"
    run:
        cell_uuid = load_yaml(str(input))['cell_uuid']
        shell("smrtlink_pdf_report.py -o {output} --empty_on_missing {cell_uuid}")

# Invokes PanDoc with the template stuff
localrules: pan_to_html
rule pan_to_html:
    output: "{report}.pan.html"
    input:  "{report}.pan"
    params:
        templates = os.environ.get('TEMPLATES', '.')
    shell:
       r"""{TOOLBOX} pandoc -f markdown \
                                --template={params.templates}/template.html \
                                --include-in-header={params.templates}/javascript.js.html \
                                --include-in-header={params.templates}/local.css.html \
                                --toc --toc-depth=2 \
                                -o {output} {input}
        """

# This rule marshalls all the .png images and makes one report in PanDoc format.
rule make_report:
    output:
        pan     = "all_reports/{cellidx}_{cell}.pan",
        imglist = "all_reports/img/{cellidx}_{cell}.list",
    input:
        yaml       = "{cell}.info.yml",
        links      = "{cell}.link.yml",
        pdf        = "all_reports/pdf/{cell}.pdf",
        blob_plots = lambda wc: glob("blob/{wc.cell}.*.png"),
    params:
        pdflink    = "pdf/{cell}.pdf",
    shell:
       r"""printf '%s\n' {input.blob_plots} > {output.imglist}
           ln -srnt all_reports/img {input.blob_plots}
           make_report.py -o {output.pan} \
                {input.yaml} {input.links} {params.pdflink} \
                -s <(pb_run_status.py)
        """
